apply plugin: 'com.android.application'

// 获取keystore相关参数
def signing = { key ->
    Properties localProperties = new Properties()
    localProperties.load(file('../local.properties').newDataInputStream())
    Properties keystoreParam = new Properties();
    keystoreParam.load(file(localProperties['keystore.properties']).newDataInputStream())
    keystoreParam[key]
}

// for version code
def tmpVerCodeByGit = {
    def versionNumberCmd = 'git rev-list HEAD --count'
    versionNumberCmd.execute().text.trim().toInteger()
}
// for version name
def tmpVerNameByGit = {
    def revisionDescriptionCMD = 'git describe'
    revisionDescriptionCMD.execute().text.trim()
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'
    defaultConfig {
        applicationId "cn.cj.media.video.player"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        debug {

        }
        release {
            storeFile file(signing('storeFile'))
            keyAlias signing('keyAlias')
            keyPassword signing('keyPassword')
            storePassword signing('storePassword')
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        applicationVariants.all { variant ->
            if (variant.buildType.name.equals('release')) {
                def vCode = tmpVerCodeByGit()
                def vName = tmpVerNameByGit();
//                println("app before converting -> VersionCode:" + vCode + ", VersionName:" + vName);
                def matcher = vName =~ /\d+-\d+-/ // 判断‘v1.1' or 'v1.1-1-g123df6b'
                if (matcher) {    // regexp
                    def s = vName.substring(1)     // remove first char 'v' of tag 'v1.1-1-g123df6b'
                    def a = s.split("-")           // split it with '-'
                    vName = a[0] + '.' + a[1]      // concatenate them for versionName
                } else {
                    vName = vName + ".0";          // eg. '1.1' + '.0' = '1.1.0'
                }
                println("app after converting VersionCode:" + vCode + ", VersionName:" + vName);
                variant.mergedFlavor.versionCode = vCode
                variant.mergedFlavor.versionName = vName

                // rename apk file
                def suff = new Date().format("yyMMddHH")
                vName = vName + "-" + suff
                def file = variant.outputs[0].outputFile // 低版本的gradle可能为variant.outputFile
                variant.outputs[0].outputFile = new File(file.parent, file.name.replace('-release.apk', "-" + vName + ".apk"))
            }
        }

    }

    productFlavors {

    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.github.june545:AndroidFloatingView:v1.0.0'
}